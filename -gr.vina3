// Bol Processor BP3
// Grammar saved as "-gr.vina3". Date: 2024-07-24 19:18:32
-to.Vina
-se.Vina3
-cs.Vina

_mm(60)

// "Pallavi" of song "Maayaatiita Svaruupini, Nannu brovave" in raga "mayamaalavagaula"
// scale - C, C#, E, F, G , G#, B
// Note phrase variations given.

// Terms:
// Spuritam - Quick (discrete) two note succession.
// Taalam - Measure of time (played on vina using the 3 sympathetic strings tuned to C-G-C).
// Gamaka - Smooth motion between two pitches (usually pull of string).
// Pallavi - Theme of composition

RND

gram#1[1] S --> _ins(Vina) _scale(just intonation,0) _volume(40) {Taalam4 Taalam4, Pallavi Pallavi} _volumecont _volume(64) Taalam _volume(0) -

gram#1[2] Pallavi --> {Maa Yaa Tii . Ta {2, Sva Ruu Pi} . {2, Ni} {1, Nan Nu} . Bro {2, Va Ve} }

gram#1[3] Maa --> C3
gram#1[4] Yaa --> LongSpuritam C3 Slide1
gram#1[5] Tii --> {1, StartSlidePull C3 Slide1 _ _ Slide1 Pull0 _ Slide4 . Pull1 _ Pull0 _ . Pull0 _ Pull1 - StopSlidePull}
gram#1[6] Ta --> F3
gram#1[7] Sva --> {3, G3}
gram#1[8] Ruu --> {3, StartSlidePull G3 SlideBack2 _ SlideBack3 . Pull0 _ Pull1 - - - StopSlidePull}
// gram#1[8] Ruu --> {3, StartSlidePull G3 SlideBack2 _ SlideBack3 _ SlideBack2 _ . _ _ SlideBack3 Pull0 _ Pull1 - StopSlidePull}
gram#1[9] Pi --> {2, F3}
gram#1[10] Ni --> G3

gram#1[11] Nan --> G3
gram#1[12] Nu --> {1, StartPull Pull1 G3 Pull0 _ Pull1 - StopPull}

gram#1[13] Bro --> {1, StartSlide F3 Slide2 _ _ _ StopSlide} /slideVa/
gram#1[14] Bro --> {1, GamakaHold F3 Pull2} /pullVa/
gram#1[15] /slideVa/ Va --> StartSlide G3 SlideBack2 StopSlide
gram#1[16] /pullVa/ Va --> StartPull Pull2 F3 Pull0 StopPull

gram#1[17] Ve --> {3, Ve1 Ve2 Ve3}
gram#1[18] Ve1 --> {5, GamakaOscil E3 Pull1}
gram#1[19] Ve2 --> StartSlide C#3 SlideBack1 _ StopSlide
gram#1[20] Ve3 --> StartPull B2 Pull1 StopPull

-----------------
RND

gram#2[1] Taalam4 --> Taalam Taalam Taalam Taalam
gram#2[2] Taalam --> {3, C3, G3, C4}

-----------------
SUB1

gram#3[1] LongSpuritam ?1 ?2 --> {1, StartSlide ?1 _ Slide0 _ ?2 _}
gram#3[2] GamakaOscil ?1 ?2 --> {StartPull ?1 ?2 _ Pull0 _ ?2 _ Pull0 _ ?2 StopPull}
gram#3[3] GamakaHold ?1 ?2 --> {1, StartPull ?1 ?2 _ _ _ StopPull}

----------------
SUB1

gram#4[1] StartSlidePull --> StartSlide StartPull
gram#4[2] StopSlidePull --> StopSlide StopPull

----------------
SUB1

// Lowest level structures

gram#5[1] Pull0 --> _pitchbend(0)
gram#5[2] Pull1 --> _pitchbend(100)
gram#5[3] Pull2 --> _pitchbend(200)

gram#5[4] Slide0 --> _value(slide, 0)
gram#5[5] SlideBack1 --> _value(slide, -101)
gram#5[6] SlideBack2 --> _value(slide, -201)
gram#5[7] SlideBack3 --> _value(slide, -301)
gram#5[8] Slide1 --> _value(slide, 101)
gram#5[9] Slide2 --> _value(slide, 201)
gram#5[10] Slide4 --> _value(slide, 401)

gram#5[11] StartSlide --> _cont(slide) _value(slide,0)
gram#5[12] StopSlide --> _fixed(slide)
gram#5[13] StartPull --> _pitchcont _pitchrange(500) _pitchbend(0)
gram#5[14] StopPull --> _pitchfixed