// Bol Processor BP3 compatible with version BP2.9.8
// Grammar file saved as "-gr.acceleration". Date: 2022-02-18 15:14:04
// A grammar by Harm Visser
// October 1997

// This grammar produces a “sawtooth-rhythm”… I have used an
// interesting way of recursion. Furthermore: I 'rewrite'
// BP2-notation-possibility (C3, do4, etc), in a more numerical
// notation; its handy when you want to use a more algorithmic
// approach. (Harm)

-se.Visser2

ORD

gram#1[1] S --> _transpose(12) _vel(60) A B _vel(65) C D _vel(70) E F _vel(75) G _vel(77) H _vel(80) I _vel(85) J _vel(87) K _vel(90) L
gram#1[2] A --> E2 . 
gram#1[3] B --> D2 A
gram#1[4] C --> B2 B
gram#1[5] D --> G2 C
gram#1[6] E --> F#2 D
gram#1[7] F --> A#2 E
gram#1[8] G --> C2 F
gram#1[9] H --> G#2 G
gram#1[10] I --> A2 H
gram#1[11] J --> D#2 I
gram#1[12] K --> C#2 J
gram#1[13] L --> F2 K